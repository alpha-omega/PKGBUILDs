# Maintainer (Spanish version): OmeGa <omega {at} mailoo {dot} org>
# Maintainer (English version): Max Roder <maxroder@web.de>

pkgname=tor-browser-es
pkgver=3.6
pkgrel=1
pkgdesc="Navegación anónima utilizando Firefox y Tor"
arch=('i686' 'x86_64')
url="https://www.torproject.org/projects/torbrowser.html"
license=('GPL')
depends=('dbus-glib' 'gtk2')
optdepends=('kdebase-kdialog: KDE dialog boxes'
            'libnotify: Gnome dialog boxes'
            'zenity: simple dialog boxes')
install=$pkgname.install
_language=es-ES

if [[ $CARCH == "x86_64" ]]; then
  _arch=linux64
else
  _arch=linux32
fi

source=("https://www.torproject.org/dist/torbrowser/$pkgver/tor-browser-$_arch-${pkgver}_${_language}.tar.xz"
        "https://www.torproject.org/dist/torbrowser/$pkgver/tor-browser-$_arch-${pkgver}_${_language}.tar.xz.asc"
        "$pkgname.desktop"
        "$pkgname.png"
        "$pkgname.sh")
noextract=(tor-browser-$_arch-${pkgver}_${_language}.tar.xz)

if [[ $CARCH == "x86_64" ]]; then
  sha1sums=('34cec0c8761c8a6bce2baeeec5d67521a314b99b'
            'ece0c27ab87cb721cd378ff1c5748ae88d02d1a9')
else
  sha1sums=('6ba8482fbad2fa8edbf95b1df38e45e3170d4850'
            '6f5124ec54a269ab53783832fa07b1ce1dc2a8fe')
fi
sha1sums+=('28e65f7537e7dc76af475166365ad0deabb91cb9'
           '7a283313a21bb9cd8331891f99d2646186231636'
           '568eeee1c1798514c749adaad3f9d6eaf0e12d54')

package() {
  cd "$srcdir"

  sed -i "s/REPL_NAME/$pkgname/g"       $pkgname.sh
  sed -i "s/REPL_LANGUAGE/$_language/g" $pkgname.sh
  sed -i "s/REPL_VERSION/$pkgver/g"     $pkgname.sh

  sed -i "s/REPL_NAME/$pkgname/g"       $pkgname.desktop
  sed -i "s/REPL_COMMENT/$pkgdesc/"     $pkgname.desktop
  sed -i "s/REPL_LANGUAGE/$_language/"  $pkgname.desktop

  install -Dm755 $pkgname.sh      "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.png     "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"

  install -Dm644 tor-browser-$_arch-${pkgver}_${_language}.tar.xz \
    "$pkgdir/opt/$pkgname/tor-browser-$_arch-${pkgver}_${_language}.tar.xz"
}

# vim:set ts=2 sw=2 et:
