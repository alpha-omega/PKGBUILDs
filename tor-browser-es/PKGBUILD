# Maintainer (Spanish version): OmeGa <omega at mailoo dot org>
# Maintainer (English version): Max Roder <maxroder@web.de>

pkgname=tor-browser-es
pkgver=2.3.25_2
_realpkgver=2.3.25-2-dev
_language=es-ES
pkgrel=1
pkgdesc="Navegación anónima utilizando Firefox y Tor"
arch=('i686' 'x86_64')
url="https://www.torproject.org/projects/torbrowser.html"
license=('GPL')
depends=('java-runtime')
optdepends=('zenity: simple dialog boxes'
            'kdialog: KDE dialog boxes'
            'libnotify: Gnome dialog boxes')
install=$pkgname.install
source=(https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz
        https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz.asc
        https://raw.github.com/alpha-omega/pkgbuild/master/$pkgname/$pkgname.desktop
        https://raw.github.com/alpha-omega/pkgbuild/master/$pkgname/$pkgname.png
        https://raw.github.com/alpha-omega/pkgbuild/master/$pkgname/$pkgname.sh)
noextract=(tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz)
if [[ "$CARCH" == 'x86_64' ]]; then
  sha256sums=('75c81a3a835f877aee23514c2a76db1896ca02f67e8466a6ef0df987d87728ed'
              'cd7ed33a89290b4ca43f247fcf5fdbb77ea170f4540083cf73b5be95e3f6072f')
else
  sha256sums=('942d5c6c2e50bea81b4eadb61e8a18f9ae8539935e0773e9e0cb97b3663b5910'
              'b907ae6ce050fc38577d01e5dc9a51fcf6111d0df1891718f31b98d372b59f36')
fi
sha256sums+=('e73b5788d195e359d7d3621db402998fb4bfca49caf5fc5814b7c2839f93b5f1'
             '17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
             '7fc947b8ae1483c5abb4545f6e26b315ff1fb16eb7ae8837f7afb10a9277383b')

package() {
  cd "$srcdir"

  sed -i "s/REPL_NAME/$pkgname/g" $pkgname.sh
  sed -i "s/REPL_VERSION/$_realpkgver/g" $pkgname.sh
  sed -i "s/REPL_LANGUAGE/$_language/g" $pkgname.sh

  sed -i "s/REPL_NAME/$pkgname/g" $pkgname.desktop
  sed -i "s/REPL_LANGUAGE/$_language/" $pkgname.desktop
  sed -i "s/REPL_COMMENT/$pkgdesc/" $pkgname.desktop

  install -Dm755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 $pkgname.png "$pkgdir/usr/share/pixmaps/$pkgname.png"

  install -Dm644 tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz \
    "$pkgdir/opt/$pkgname/tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz"
}

# vim:set ts=2 sw=2 et:
