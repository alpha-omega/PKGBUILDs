# Maintainer (Spanish version): OmeGa <omega {at} mailoo {dot} org>
# Maintainer (English version): Max Roder <maxroder@web.de>

pkgname=tor-browser-es
pkgver=3.5.2.1
pkgrel=1
pkgdesc="Navegación anónima utilizando Firefox y Tor"
arch=('i686' 'x86_64')
url="https://www.torproject.org/projects/torbrowser.html"
license=('GPL')
depends=('dbus-glib' 'gtk2')
optdepends=('kdebase-kdialog: KDE dialog boxes'
            'libnotify: Gnome dialog boxes'
            'zenity: simple dialog boxes')
install=$pkgname.install
_language=es-ES

if [[ $CARCH == "x86_64" ]]; then
  _arch=linux64
else
  _arch=linux32
fi

source=("https://www.torproject.org/dist/torbrowser/$pkgver/tor-browser-$_arch-${pkgver}_${_language}.tar.xz"
        "https://www.torproject.org/dist/torbrowser/$pkgver/tor-browser-$_arch-${pkgver}_${_language}.tar.xz.asc"
        "$pkgname.desktop"
        "$pkgname.png"
        "$pkgname.sh")
noextract=(tor-browser-$_arch-${pkgver}_${_language}.tar.xz)

if [[ $CARCH == "x86_64" ]]; then
  sha256sums=('c51397f8e9b853ef36515407fe2f03e5ad94fb4fcd59945897c1d94e5c3f6771'
              '8f54555e622e368a0b86a2243dfcad3391568176aa3fcb11ce0bf30d3c6c6734')
else
  sha256sums=('9d76f117819e39584b6a63733408241d324aeb786037255fdc7bb3fa7d330c5b'
              '5b0e37b4f225040f5181168185a5f3a1ce6caef70598113e84cbe86a6704192a')
fi
sha256sums+=('e73b5788d195e359d7d3621db402998fb4bfca49caf5fc5814b7c2839f93b5f1'
             '17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
             '3d27e4148091c815385271accc795a2ad8c70acdaca5629ce535d8b55362ccd4')

package() {
  cd "$srcdir"

  sed -i "s/REPL_NAME/$pkgname/g"       $pkgname.sh
  sed -i "s/REPL_LANGUAGE/$_language/g" $pkgname.sh
  sed -i "s/REPL_VERSION/$pkgver/g"     $pkgname.sh

  sed -i "s/REPL_NAME/$pkgname/g"       $pkgname.desktop
  sed -i "s/REPL_COMMENT/$pkgdesc/"     $pkgname.desktop
  sed -i "s/REPL_LANGUAGE/$_language/"  $pkgname.desktop

  install -Dm755 $pkgname.sh      "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.png     "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"

  install -Dm644 tor-browser-$_arch-${pkgver}_${_language}.tar.xz \
    "$pkgdir/opt/$pkgname/tor-browser-$_arch-${pkgver}_${_language}.tar.xz"
}

# vim:set ts=2 sw=2 et:
