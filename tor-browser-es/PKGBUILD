# Maintainer (Spanish version): OmeGa <omega at mailoo dot org>
# Maintainer (English version): Max Roder <maxroder@web.de>

pkgname=tor-browser-es
pkgver=2.3.25_3
_realpkgver=2.3.25-3-dev
_language=es-ES
pkgrel=1
pkgdesc="Navegación anónima utilizando Firefox y Tor"
arch=('i686' 'x86_64')
url="https://www.torproject.org/projects/torbrowser.html"
license=('GPL')
depends=('java-runtime')
optdepends=('zenity: simple dialog boxes'
            'kdialog: KDE dialog boxes'
            'libnotify: Gnome dialog boxes')
install=$pkgname.install
source=(https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz
        https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz.asc
        $pkgname.desktop
        $pkgname.png
        $pkgname.sh)
noextract=(tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz)
if [[ $CARCH == x86_64 ]]; then
  sha256sums=('768d249887fd158c2e8a3a9923ab4e4c139a682db92a67ad6a5261d2414d7f48'
              '1b698c9d803f3367d2490d07e0dabc3f67c4708d509ff55f6a6c655237aa59e2')
else
  sha256sums=('77510bc0cd29bebbb5e27828317672f6d6f93f147fa79479a6574844a5c097db'
              'd1037a57e549aa8e44b6d7c6b2e6481f397e0c1f0cf978307fa8a20eed89710f')
fi
sha256sums+=('e73b5788d195e359d7d3621db402998fb4bfca49caf5fc5814b7c2839f93b5f1'
             '17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
             '7fc947b8ae1483c5abb4545f6e26b315ff1fb16eb7ae8837f7afb10a9277383b')

package() {
  cd "$srcdir"

  sed -i "s/REPL_NAME/$pkgname/g"         $pkgname.sh
  sed -i "s/REPL_LANGUAGE/$_language/g"   $pkgname.sh
  sed -i "s/REPL_VERSION/$_realpkgver/g"  $pkgname.sh

  sed -i "s/REPL_NAME/$pkgname/g"       $pkgname.desktop
  sed -i "s/REPL_COMMENT/$pkgdesc/"     $pkgname.desktop
  sed -i "s/REPL_LANGUAGE/$_language/"  $pkgname.desktop

  install -Dm755 $pkgname.sh      "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.png     "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"

  install -Dm644 tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz \
    "$pkgdir/opt/$pkgname/tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz"
}

# vim:set ts=2 sw=2 et:
