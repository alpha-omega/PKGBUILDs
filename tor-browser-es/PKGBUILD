# Maintainer (Spanish version): OmeGa <omega at mailoo dot org>
# Maintainer (English version): Max Roder <maxroder@web.de>

pkgname=tor-browser-es
pkgver=3.5.1
pkgrel=1
pkgdesc="Navegación anónima utilizando Firefox y Tor"
arch=('i686' 'x86_64')
url="https://www.torproject.org/projects/torbrowser.html"
license=('GPL')
optdepends=('kdebase-kdialog: KDE dialog boxes'
            'libnotify: Gnome dialog boxes'
            'zenity: simple dialog boxes')
install=$pkgname.install
_language=es-ES

if [[ $CARCH == "x86_64" ]]; then
  _arch=linux64
else
  _arch=linux32
fi

source=("https://www.torproject.org/dist/torbrowser/$pkgver/tor-browser-$_arch-${pkgver}_${_language}.tar.xz"
        "https://www.torproject.org/dist/torbrowser/$pkgver/tor-browser-$_arch-${pkgver}_${_language}.tar.xz.asc"
        "$pkgname.desktop"
        "$pkgname.png"
        "$pkgname.sh")
noextract=(tor-browser-$_arch-${pkgver}_${_language}.tar.xz)

if [[ $CARCH == "x86_64" ]]; then
  sha256sums=('2379bdefd09a9506b9640ca1e3d75ffd2b8291be64b8af0b50b575cda9788aa9'
              '688b5c3cd37cd0122868fdaa57246a79c963ef98430ec5b2b3632bf996749d24')
else
  sha256sums=('baea4410f7238b511a974038ae3d0c8479a364715ee1f88b9f0152b32c0febbf'
              '4e9c70724bd9dca4ee5ae6f45c23f427a2c0f8f76f0189505bb4b34c402904e7')
fi
sha256sums+=('e73b5788d195e359d7d3621db402998fb4bfca49caf5fc5814b7c2839f93b5f1'
             '17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
             '3d27e4148091c815385271accc795a2ad8c70acdaca5629ce535d8b55362ccd4')

package() {
  cd "$srcdir"

  sed -i "s/REPL_NAME/$pkgname/g"       $pkgname.sh
  sed -i "s/REPL_LANGUAGE/$_language/g" $pkgname.sh
  sed -i "s/REPL_VERSION/$pkgver/g"     $pkgname.sh

  sed -i "s/REPL_NAME/$pkgname/g"       $pkgname.desktop
  sed -i "s/REPL_COMMENT/$pkgdesc/"     $pkgname.desktop
  sed -i "s/REPL_LANGUAGE/$_language/"  $pkgname.desktop

  install -Dm755 $pkgname.sh      "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.png     "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"

  install -Dm644 tor-browser-$_arch-${pkgver}_${_language}.tar.xz \
    "$pkgdir/opt/$pkgname/tor-browser-$_arch-${pkgver}_${_language}.tar.xz"
}

# vim:set ts=2 sw=2 et:
