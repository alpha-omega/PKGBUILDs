# Maintainer: OmeGa <omega at mailoo dot org>
# Contributor: Vinycius Maia <suportevg@uol.com.br>

pkgname=firefox-extension-greasemonkey
_file=193904
pkgver=1.8
pkgrel=1
pkgdesc="Customize the way a web page displays or behaves, by using small bits of JavaScript."
arch=('any')
url="http://www.greasespot.net/"
license=('MPL')
depends=('firefox>=14.0')
makedepends=('unzip')
source=(https://addons.mozilla.org/firefox/downloads/file/$_file/greasemonkey-$pkgver.xpi)
noextract=(greasemonkey-$pkgver.xpi)
sha1sums=('4f592d17a7bd939513f656f52dba273ea0b4e4a4')

build() {
  cd "$srcdir"
  [[ -d $pkgname-extract ]] || \
    mkdir $pkgname-extract
  unzip -q -d $pkgname-extract \
    greasemonkey-$pkgver.xpi
}

package() {
  cd "$srcdir/$pkgname-extract"
  local emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf)
  local dstdir="$pkgdir/usr/lib/firefox/extensions/$emid"
  install -d "$dstdir"
  cp -r * "$dstdir"
  find "$pkgdir" -type d -exec chmod 0755 {} \;
  find "$pkgdir" -type f -exec chmod 0644 {} \;
}

# vim:set ts=2 sw=2 et:
