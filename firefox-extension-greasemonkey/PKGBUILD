# Maintainer: OmeGa <omega [U+0040] mailoo dot org>
# Contributor: Vinycius Maia <suportevg@uol.com.br>

pkgname=firefox-extension-greasemonkey
_file=243212
pkgver=1.15
pkgrel=1
pkgdesc="Customize the way a web page displays or behaves, by using small bits of JavaScript."
arch=('any')
url="http://www.greasespot.net/"
license=('MPL')
depends=('firefox')
makedepends=('unzip')
source=(https://addons.mozilla.org/firefox/downloads/file/$_file/greasemonkey-$pkgver.xpi)
noextract=(greasemonkey-$pkgver.xpi)
sha1sums=('085457fc5cd30ae908e30a14ad350a4bed61e558')

build() {
  cd "$srcdir"
  [[ -d $pkgname-extract ]] || mkdir $pkgname-extract
  unzip -q -d $pkgname-extract greasemonkey-$pkgver.xpi
}

package() {
  cd "$srcdir/$pkgname-extract"
  local emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf)
  local dstdir="$pkgdir/usr/lib/firefox/browser/extensions/$emid"
  find . -type f -exec install -Dm644 '{}' "$dstdir/{}" \;
}

# vim:set ts=2 sw=2 et:
