# Maintaniner: OmeGa <omega at mailoo dot org>

pkgname=firefox-spell-en-us
_file=150880
pkgver=6.0
pkgrel=2
pkgdesc="English (US) spellchecking dictionary for Firefox."
arch=('any')
url="https://addons.mozilla.org/firefox/addon/3497/"
license=('MPL')
depends=('firefox')
makedepends=('unzip')
source=($pkgname-$pkgver.xpi::https://addons.mozilla.org/firefox/downloads/file/$_file/united_states_english_spellchecker-$pkgver.xpi)
noextract=($pkgname-$pkgver.xpi)
sha1sums=('478c4a958f79e1c60d1d03216a02efbead5c9d2e')

build() {
  cd "$srcdir"
  [[ -d $pkgname-extract ]] || \
    mkdir $pkgname-extract
  cd $pkgname-extract
  unzip -q ../$pkgname-$pkgver.xpi
}

package() {
  cd "$srcdir/$pkgname-extract"
  local emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf) || return 1
  local dstdir="$pkgdir/usr/lib/firefox/extensions/$emid"
  install -d "$dstdir"
  sed -i 's#<em:maxVersion>.*</em:maxVersion>#<em:maxVersion>20.*</em:maxVersion>#' install.rdf
  cp -R * "$dstdir"
  find "$pkgdir" -type d -exec chmod 0755 {} \;
  find "$pkgdir" -type f -exec chmod 0644 {} \;
}

# vim:set ts=2 sw=2 et:
