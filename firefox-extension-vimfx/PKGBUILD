# Maintainer: OmeGa <omega [U+0040] mailoo [dot] org>

pkgname=firefox-extension-vimfx
_name=vimfx
_file=238500
pkgver=0.5.5
pkgrel=1
pkgdesc="Introduces Vim-style keyboard shortcuts for browsing and navigation without using the mouse."
arch=('any')
url="https://github.com/akhodakivskiy/VimFx"
license=('MIT')
depends=('firefox>=25.0')
makedepends=('unzip')
source=(https://addons.mozilla.org/firefox/downloads/file/$_file/$_name-$pkgver.xpi
        LICENSE)
noextract=($_name-$pkgver.xpi)
sha1sums=('60f97f861cb612c79186ae2215fa202fb658a8a6'
          'f1eaf939b37946dea611a5d55ef4784306c70a53')

build() {
  [[ -d $pkgname-extract ]] || mkdir $pkgname-extract
  unzip -q -d $pkgname-extract $_name-$pkgver.xpi
}
  
package() {
  cd "$pkgname-extract"
  local emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf)
  local dstdir="$pkgdir/usr/lib/firefox/browser/extensions/$emid"
  find . -type f -exec install -Dm644 '{}' "$dstdir/{}" \;

  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
